buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
    }
}

apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  maven { url "http://dvs1.progwml6.com/files/maven" }
  maven { url "http://tehnut.info/maven" }
  maven { url "http://maven.blamejared.com/" }
}

ext.buildnumber=""
if (System.getenv().BUILD_NUMBER) {
    ext.buildnumber="-" + System.getenv().BUILD_NUMBER
}

version = "${minecraft_version}-${mod_version}${buildnumber}"
group= "uk.joshiejack.harvest"
archivesBaseName = "Harvest-Festival"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"
    coreMod = "joshie.harvest.asm.HFTransformer"

    replace "@VERSION@", "${mod_version}"
    replaceIn "HFModInfo.java"
}

dependencies {
  deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}"
  deobfCompile "info.tehnut.buttons:Buttons:${minecraft_version}-${buttons_version}"
  deobfCompile "MineTweaker3:MineTweaker3-API:${minetweaker_version}"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'harvestfestival_at.cfg'
        attributes 'FMLCorePlugin': 'joshie.harvest.asm.HFTransformer'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
	exclude 'joshie/harvest/debug'
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'joshie/harvest/api/**/*'
    classifier = 'api'
}

tasks.build.dependsOn apiJar

//curseforge {
//    if (System.getenv().CURSE_API) {
//        apiKey = System.getenv().CURSE_API
//    }
//
//    project {
//        id = "${curse_id}"
//        releaseType = 'alpha'
//        changelog = 'WARNING! This is highly experimental, with missing textures so use at your own risk!'
//        addGameVersion "${minecraft_version}"
//        addArtifact(apiJar)
//    }
//}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId "harvest_festival_${minecraft_version}"
            artifact jar
            artifact apiJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

idea { module { inheritOutputDirs = true } }
